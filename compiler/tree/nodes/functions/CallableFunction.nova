package "compiler/tree/nodes/functions"

import "compiler/tree/nodes/Node"
import "compiler/tree/nodes/Identifier"
import "compiler/InvalidParseException"
import "compiler/tree/nodes/annotations/Annotation"
import "compiler/tree/AnnotationSearchResult"
import "compiler/tree/nodes/generics/GenericParameter"
import "compiler/tree/nodes/generics/GenericCompatible"
import "compiler/tree/nodes/Value"
import "compiler/tree/SyntaxTree"
import "compiler/SyntaxMessage"

import "compiler/util/CompilerStringFunctions"

interface CallableFunction extends GenericCompatible {
    visible ParameterList parameterList => null
    visible Parameter[] parameters => parameterList.parameters
    
    public parseParameter(String parameter, Bool require = true) =>
        Parameter.parse(parameter, parameterList, ((Node)this).location)
    
    public parseParameterAndAnnotations(String parameter, Bool require = true) => false {
        var annotations = SyntaxTree.searchAnnotations(parameter)
        var param = parseParameter(annotations.remainingStatement, require)
        
        if (param != null) {
            parameters.add(param)
            annotations.addTo(param)
            
            return true
        }
        
        SyntaxMessage.errorIf("Invalid parameter '#parameter'", (Node)this, require)
    }
    
    public parseParameters(String parameters, Bool require = true) =>
        parameters.splitAtCommas().all({ parseParameterAndAnnotations(_, require) }, false)
    
    public compatibleArguments(Value[] arguments) -> Bool {
        return true
    }
    
    public parseName(String name) => false {
        if (!parseGenericParameters(name)) {
            softly throw new InvalidParseException("Unable to parse generic parameters for class '#name'", (Node)this)
        } else {
            if (genericParameters.count > 0) {
                name = name.substring(end: name.indexOf('<')).trim()
            }
            
            ((Identifier)this).name = name
            
            return name.isIdentifier
        }
    }
}