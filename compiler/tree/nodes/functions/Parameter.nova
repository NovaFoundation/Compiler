package "compiler/tree/nodes/functions"

import "compiler/tree/nodes/Node"
import "compiler/tree/nodes/Value"
import "compiler/tree/nodes/PlaceholderValue"
import "compiler/tree/nodes/variables/LocalDeclaration"
import "compiler/tree/nodes/functions/closures/ClosureDeclaration"
import "compiler/util/Location"

import "compiler/util/CompilerStringFunctions"

class Parameter extends LocalDeclaration {
    public Value defaultValue
    
    visible ParameterList parentParameterList =>
        parent.class.isOfType(ParameterList) ? (ParameterList)parent : null
    
    /// Create a new Parameter node.
    /// 
    /// #exampleInputs
    /// 
    /// parent:   The parent of the Parameter node.
    /// location: The location of the input text in the source file.
    public construct(Node parent = null, Location location = Location.INVALID) {
        super(parent, location)
    }
    
    /// Parse the given input into a Parameter node. If the input cannot be parsed
    /// into a Parameter node, null is returned.
    /// 
    /// #exampleInputs:
    ///     Example inputs:
    ///      * 
    ///      * 
    ///      * 
    /// 
    /// input:    The input string to parse into a Parameter instance.
    /// parent:   The parent of the Parameter node.
    /// location: The location of the input text in the source file.
    /// require:  Whether or not to throw a syntax error if the input could not
    ///           be parsed.
    /// return:   The parsed Parameter node, if successful; null otherwise.
    public static parse(String input, Node parent = null, Location location = Location.INVALID, Bool require = true) -> Parameter {
        var closure = ClosureDeclaration.parse(input, parent, location, require)
        
        if (closure != null) {
            return closure
        }
        
        String defaultValue = null
        var assignmentIndex = input.indexOf('=')
        
        if (assignmentIndex > 0 && input.isStrictlyOperator('=', assignmentIndex)) {
            defaultValue = input.substring(assignmentIndex + 1).trim()
            
            input = input.substring(end: assignmentIndex).trim()
        }
        
        var declaration = LocalDeclaration.parse(input, parent, location, require)
        
        if (declaration != null) {
            var node = new Parameter(parent, location)
            
            if (defaultValue != null) {
                node.defaultValue = PlaceholderValue.parse(defaultValue, node, location, require)
            }
            
            return (Parameter)declaration.cloneTo(node)
        }
        
        return null
    }
    
    public parsePlaceholders() =>
        defaultValue.class.isOfType(PlaceholderValue) ?
            (defaultValue = Value.parse((PlaceholderValue)defaultValue)) != null :
            true
    
    public cloneTo(Parameter other) => other {
        super.cloneTo(other)
        
        other.defaultValue = defaultValue
    }
}