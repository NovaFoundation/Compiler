package "compiler/tree/nodes/functions"

import "compiler/SyntaxMessage"
import "compiler/tree/nodes/Node"
import "compiler/tree/nodes/variables/InstanceDeclaration"
import "compiler/util/Location"

import "compiler/util/CompilerStringFunctions"

class FunctionDeclaration extends InstanceDeclaration {
    visible FunctionDeclaration parentFunction => this
    
    visible ParameterList parameterList
    visible Parameter[] parameters => parameterList.parameters
    
    /// <summary>
    /// Create a new FunctionDeclaration node.<br><br>
    /// <include id="example-inputs">
    /// </summary>
    /// 
    /// <param name="parent">The parent of the FunctionDeclaration node.</param>
    /// <param name="location">The location of the input text in the source file.</param>
    public construct(Node parent = null, Location location = Location.INVALID) {
        super(parent, location)
        
        parameterList = new ParameterList(parent, location)
    }
    
    /// <summary>
    /// Decode the given input into a FunctionDeclaration node. If the input cannot be parsed
    /// into a FunctionDeclaration node, null is returned.<br><br>
    /// <div id="example-inputs">
    ///     Example inputs:
    ///     <ul>
    ///         <li></li>
    ///         <li></li>
    ///         <li></li>
    ///     </ul>
    /// </div>
    /// </summary>
    /// 
    /// <param name="input">The input string to decode into a FunctionDeclaration instance.</param>
    /// <param name="parent">The parent of the FunctionDeclaration node.</param>
    /// <param name="location">The location of the input text in the source file.</param>
    /// <param name="require">Whether or not to throw a syntax error if the input could not
    ///     be parsed.</param>
    /// <returns>The parsed FunctionDeclaration node, if successful; null otherwise.</returns>
    public static parse(String input, Node parent = null, Location location = Location.INVALID, Bool require = true) -> FunctionDeclaration {
        var signature  = findSignature(input)
        var parameters = findParameters(input)
        var returnType = findReturnType(input)
        
        if (signature != null && parameters != null && returnType != null) {
            var node = new FunctionDeclaration(parent, location)
            
            if (node.parseSignature(signature, require) &&
                node.parseParameters(parameters, require) &&
                (returnType.count == 0 || node.parseType(returnType))) {
                return node
            }
        }
        
        if (require) {
            SyntaxMessage.error("Invalid function declaration '#input'", parent)
        }
        
        return null
    }
    
    static findSignature(String input) -> String {
        var start = input.indexOf('(')
        
        if (start < 0) {
            return null
        }
        
        return input.substring(end: start).trim()
    }
    
    static findReturnType(String input) -> String {
        var start = input.indexOf('(')
        
        if (start < 0) {
            return null
        }
        
        var end = input.findEndingMatch(start, '(', ')')
        
        if (end < 0) {
            return null
        }
        
        if (input.containsAllWhitespaceAfter(end + 1)) {
            return ""
        }
        
        end = input.nextWordIndex("->", end + 1)
        
        if (end < 0) {
            return null
        }
        
        return input.substring(start: end + 2).trim()
    }
    
    static findParameters(String input) -> String {
        var start = input.indexOf('(')
        
        if (start < 0) {
            return null
        }
        
        var end = input.findEndingMatch(start, '(', ')')
        
        if (end < 0) {
            return null
        }
        
        return input.substring(start + 1, end).trim()
    }
    
    public parseSignature(String signature, Bool require) =>
        signature.split(/\s+/).filterEmpty().all((word, index, words) -> {
            if (index == ((Array)words).count - 1) {
                name = word
                
                return name.isIdentifier
            }
            
            return parseModifier(word)
        })
        
    public parseParameter(String parameter, Bool require = true) -> Bool {
        var param = Parameter.parse(parameter, parameterList, location, false)
        
        if (param != null) {
            parameters.add(param)
            
            return true
        }
        
        SyntaxMessage.errorIf("Invalid parameter '#parameter'", this, require)
        
        return false
    }
    
    public parseParameters(String parameters, Bool require) =>
        parameters.splitAtCommas().all({ parseParameter(_, require) })
    
    public writeReturnType() => type != null ? " -> #type" : ""
    
    public toString() => "#writeModifiers() #name".replace(/\s+/, " ").trim() + "#parameterList" + writeReturnType()
    
    public cloneTo(FunctionDeclaration other) => other {
        super.cloneTo(other)
        
        other.parameterList = parameterList
    }
}