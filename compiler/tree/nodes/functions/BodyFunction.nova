package "compiler/tree/nodes/functions"

import "compiler/tree/nodes/Node"
import "compiler/tree/nodes/Scope"
import "compiler/util/Location"
import "compiler/SyntaxMessage"

import "compiler/util/CompilerStringFunctions"

class BodyFunction extends FunctionDeclaration {
    public Scope scope <=> scope
    
    visible String inlineBody
    
    /// <summary>
    /// Create a new BodyFunction node.<br><br>
    /// <include id="example-inputs">
    /// </summary>
    /// 
    /// <param name="parent">The parent of the BodyFunction node.</param>
    /// <param name="location">The location of the input text in the source file.</param>
    public construct(Node parent = null, Location location = Location.INVALID) {
        super(parent, location)
        
        scope = new Scope(this, location)
    }
    
    public addChild(Node child) {
        scope.children.add(child)
    }
    
    public parseStatement(String input, Node parent = null, Location location = Location.INVALID, Bool require = true) =>
        scope.parseStatement(input, this, location, require)
    
    /// <summary>
    /// Decode the given input into a BodyFunction node. If the input cannot be parsed
    /// into a BodyFunction node, null is returned.<br><br>
    /// <div id="example-inputs">
    ///     Example inputs:
    ///     <ul>
    ///         <li></li>
    ///         <li></li>
    ///         <li></li>
    ///     </ul>
    /// </div>
    /// </summary>
    /// 
    /// <param name="input">The input string to decode into a BodyFunction instance.</param>
    /// <param name="parent">The parent of the BodyFunction node.</param>
    /// <param name="location">The location of the input text in the source file.</param>
    /// <param name="require">Whether or not to throw a syntax error if the input could not
    ///     be parsed.</param>
    /// <returns>The parsed BodyFunction node, if successful; null otherwise.</returns>
    public static parse(String input, Node parent = null, Location location = Location.INVALID, Bool require = true) -> BodyFunction {
        String inlineBody = null
        var inlineBodyIndex = findInlineBodyIndex(input)
        
        if (inlineBodyIndex > 0) {
            inlineBody = input.substring(inlineBodyIndex + 2).trim()
            input = input.substring(end: inlineBodyIndex).trim()
        }
        
        var function = FunctionDeclaration.parse(input, parent, location, require)
        
        if (function != null) {
            var node = new BodyFunction(parent, location)
            node.inlineBody = inlineBody
            
            return (BodyFunction)function.cloneTo(node)
        }
        
        if (require) {
            SyntaxMessage.error("Invalid function declaration '#input'", parent)
        }
        
        return null
    }
    
    static findInlineBodyIndex(String input, Int defaultReturnValue = -1) -> Int {
        var start = input.indexOf('(')
        
        if (start < 0) {
            return defaultReturnValue
        }
        
        var end = input.findEndingMatch(start, '(', ')')
        
        if (end < 0) {
            return defaultReturnValue
        }
        
        end = input.nextWordIndex("=>", end + 1)
        
        if (end < 0) {
            return defaultReturnValue
        } else {
            return end
        }
    }
    
    public parsePlaceholders() =>
        scope.parsePlaceholders() &
        super.parsePlaceholders()
    
    public toNova() =>
        writeHeader() + " " + scope.toNova()
}