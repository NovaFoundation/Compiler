package "compiler/tree/nodes/operations"

import "compiler/SyntaxErrorException"
import "compiler/SyntaxMessage"
import "compiler/tree/nodes/Value"
import "compiler/tree/nodes/Node"
import "compiler/util/Location"

import "compiler/util/CompilerStringFunctions"

class Operation extends Value {
    visible Operator[] operators
    visible Value[] operands
    
    /// Create a new Operation node.
    /// 
    /// #exampleInputs
    /// 
    /// parent:   The parent of the Operation node.
    /// location: The location of the input text in the source file.
    public construct(Node parent = null, Location location = Location.INVALID) {
        super(parent, location)
        
        operators = new Operator[]
        operands = new Value[]
    }
    
    /// Parse the given input into a Operation node. If the input cannot be parsed
    /// into a Operation node, null is returned.
    /// 
    /// #exampleInputs:
    ///     Example inputs:
    ///      * 
    ///      * 
    ///      * 
    /// 
    /// input:    The input string to parse into a Operation instance.
    /// parent:   The parent of the Operation node.
    /// location: The location of the input text in the source file.
    /// require:  Whether or not to throw a syntax error if the input could not
    ///           be parsed.
    /// return:   The parsed Operation node, if successful; null otherwise.
    public static parse(String input, Node parent = null, Location location = Location.INVALID, Bool require = true) -> Operation {
        var matches = findOperatorIndices(input)
        
        if (matches.count > 0) {
            var node = new Operation(parent, location)
            
            if (!node.parseOperators(input, matches)){
                SyntaxMessage.invalidParse("Failed to parse operators for '#input'", node)
            } else if (!node.parseOperands(input, matches)) {
                SyntaxMessage.invalidParse("Failed to parse operands for '#input'", node)
            } else {
                return node
            }
        } else {
            SyntaxMessage.invalidParse("No operators in '#input'")
        }
        
        return null
    }
    
    static findOperatorIndices(String input) -> Int[] {
        var matches = new Int[0]
        
        Operator.BINARY_OPERATORS.forEach(operator -> {
            Int index = 0
            
            while ((index = input.findStringInBaseScope(operator, start: index + 1)) > 0) {   
                if (input.isStrictlyOperator(operator, index)) {
                    matches.add(index)
                }
            }
        })
        
        return matches
    }
    
    parseOperators(String input, Int[] matches) =>
        matches.all((start, i, starts) -> {
            var end = input.nextIndexThatDoesntContain(Operator.OPERATOR_SYMBOLS, start: start)
            
            var operator = new Operator(this, location, input.substring(start, end))
            
            if (operator != null) {
                operators.add(operator)
                
                return true
            } else {
                return false
            }
        }, false)
    
    parseOperands(String[] operands) =>
        operands.all({
            var operand = Value.parse(_, this, location)
            
            if (operand != null) {
                this.operands.add(operand)
                
                return true
            } else {
                return false
            }
        })
    
    parseOperands(String input, Int[] matches) -> Bool {
        var operands = new String[]
        
        var prev = 0
        
        if (!matches.all(start -> {
            if (prev > start) {
                return false
            } else {
                operands.add(input.substring(prev, start).trim())
                
                prev = input.nextIndexThatDoesntContain(Operator.OPERATOR_SYMBOLS, start: start)
                
                return true
            }
        })) {
            return false
        }
        
        operands.add(input.substring(prev).trim())
        
        return parseOperands(operands)
    }
    
    public toNova() -> String {
        var output = ""
        
        for (i in 0..operators.count) {
            output += "#operands[i] #operators[i] "
        }
        
        output += "#operands.last"
        
        return output
    }
}