package "compiler/tree/nodes"

import "compiler/SyntaxErrorException"
import "compiler/SyntaxMessage"
import "compiler/util/Location"
import "compiler/tree/nodes/variables/InstanceDeclaration"
import "compiler/tree/nodes/variables/FieldDeclaration"
import "compiler/tree/nodes/functions/BodyFunction"
import "nova/regex/Pattern"

import "compiler/util/CompilerStringFunctions"

/// <summary>
/// <a href="compiler/tree/nodes/InstanceDeclaration">InstanceDeclaration</a> extension that represents 
/// <summary>
///
/// <author>Braden Steffaniak</author>
/// <since></since>
/// <version></version>
class NovaClass extends InstanceDeclaration implements Abstractable {
    visible NovaClass parentClass => this
    
    public Bool isAbstract = false
    
    public String extendedClass
    public String[] implementedInterfaces = new String[0]
    
    public Scope scope <=> scope
    
    /// <summary>
    /// Create a new NovaClass node.<br><br>
    /// <include id="example-inputs">
    /// </summary>
    /// 
    /// <param name="parent">The parent of the NovaClass node.</param>
    /// <param name="location">The location of the input text in the source file.</param>
    public construct(Node parent = null, Location location = Location.INVALID) {
        super(parent, location)
        
        scope = new Scope(this, location)
    }
    
    public parseStatement(String input, Node parent = null, Location location = Location.INVALID, Bool require = true) -> Node {
        Node node = null
        
        until (node != null) {
            node = BodyFunction.parse(input, this, require: require)
            node = FieldDeclaration.parse(input, this, require: require)
        }
        
        return node
    }
    
    /// <summary>
    /// Decode the given input into a NovaClass node. If the input cannot be parsed
    /// into a NovaClass node, null is returned.<br><br>
    /// <div id="example-inputs">
    ///     Example inputs:
    ///     <ul>
    ///         <li>class MyClass</li>
    ///         <li>public class Abc extends MyClass</li>
    ///         <li>private class Xyz extends Abc implements Multiply, Divide</li>
    ///     </ul>
    /// </div>
    /// </summary>
    /// 
    /// <param name="input">The input string to decode into a NovaClass instance.</param>
    /// <param name="parent">The parent of the NovaClass node.</param>
    /// <param name="location">The location of the input text in the source file.</param>
    /// <param name="require">Whether or not to throw a syntax error if the input could not
    ///     be parsed.</param>
    /// <returns>The parsed NovaClass node, if successful; null otherwise.</returns>
    public static parse(String input, Node parent = null, Location location = Location.INVALID, Bool require = true) -> NovaClass {
        var index = input.nextWordIndex("class")
        
        if (index >= 0) {
            var node = new NovaClass(parent, location)
            
            if (!node.parseModifiers(input, index, require)) {
                return null
            }
            
            var nameStartIndex = input.nextNonWhitespaceIndex(index + 6)
            var nameEndIndex   = input.nextWhitespaceIndex(nameStartIndex + 1)
            
            if (node.parseName(input, nameStartIndex, nameEndIndex, require) && node.parseInheritence(input, nameEndIndex, require)) {
                return node
            } else if (require) {
                SyntaxMessage.error("Invalid class declaration '#input'", node)
            }
        } else if (require) {
            SyntaxMessage.error("Invalid class declaration '#input'", parent)
        }
        
        return null
    }
    
    parseModifiers(String input, Int index, Bool require) -> Bool {
        var modifiers = input.substring(end: index).trim()
        
        if (!parseModifiers(modifiers)) {
            var invalid = getInvalidModifiers(modifiers)
            
            SyntaxMessage.errorIf("Invalid modifier#{invalid.count != 1 ? "s" : ""}: '#{invalid.join("', '")}'", this, require)
            
            return false
        }
        
        return true
    }
    
    parseName(String input, Int start, Int end, Bool require) -> Bool {
        if (end > start) {
            name = input.substring(start, end)
            
            if (name.isIdentifier) {
                return true
            } else {
                SyntaxMessage.errorIf("Invalid class name '#name'", this, require)
            }
        }
        
        return false
    }
    
    parseInheritence(String input, Int nameEndIndex, Bool require) -> Bool {
        var extension       = input.substring(input.nextNonWhitespaceIndex(nameEndIndex + 1, defaultReturnValue: input.count))
        var implementations = ""
        var implementsIndex = extension.nextWordIndex("implements")
        
        if (implementsIndex >= 0) {
            implementations = extension.substring(start: implementsIndex).trim()
            extension       = extension.substring(end:   implementsIndex).trim()
        }
        
        return parseExtension(extension, require) && parseImplementations(implementations, require)
    }
    
    parseExtension(String extension, Bool require) -> Bool {
        if (extension.startsWith(/extends\s+/)) {
            extension = extension.substring("extends ".count).trim()
            
            if (!extension.isIdentifier) {
                SyntaxMessage.errorIf("Extended class '#extension' is not a valid type", this, require)
                
                return false
            }
            
            extendedClass = extension
            
            return true
        }
        
        return extension.count == 0
    }
    
    parseImplementations(String implementations, Bool require) -> Bool {
        if (implementations.startsWith(/implements\s+/)) {
            implementedInterfaces = implementations.substring("implements ".count).splitAtCommas()
            
            if (implementedInterfaces.any({ !_.isIdentifier })) {
                var invalid = implementedInterfaces.filter({ !_.isIdentifier })
                
                SyntaxMessage.errorIf("Invalid interface type#{invalid.count != 1 ? "s" : ""}: '#{invalid.join("', '")}'", this, require)
                
                return false
            }
            
            return true
        }
        
        return implementations.count == 0
    }
    
    
    
    [Override]
    public parseModifier(String modifier) -> Bool {
        if (super.parseModifier(modifier)) return true
            
        match (modifier)
            case "abstract" isAbstract = true
            default return false
        return true
    }
    
    public writeAbstract() => isAbstract ? "abstract " : ""
    public writeModifiers() => super.writeModifiers() + writeAbstract()
    public writeExtension() => extendedClass != null ? "extends #extendedClass" : ""
    public writeImplementedInterfaces() => implementedInterfaces.count > 0 ? "implements #{implementedInterfaces.join(", ")}" : ""
    
    [Override]
    public toString() => "#writeModifiers() class #name #writeExtension() #writeImplementedInterfaces()".replace(/\s+/, " ").trim()
}