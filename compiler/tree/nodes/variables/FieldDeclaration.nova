package "compiler/tree/nodes/variables"

import "compiler/tree/nodes/Node"
import "compiler/tree/nodes/Value"
import "compiler/tree/nodes/PlaceholderValue"
import "compiler/SyntaxErrorException"
import "compiler/util/Location"

import "compiler/util/CompilerStringFunctions"

class FieldDeclaration extends InstanceDeclaration {
    public Value initializationValue
    
    visible Value accessorValue
    
    visible Bool isTwoWayAccessor = false
    
    /// Create a new FieldDeclaration node.
    /// #exampleInputs
    /// 
    /// parent:   The parent of the FieldDeclaration node.
    /// location: The location of the input text in the source file.
    public construct(Node parent = null, Location location = Location.INVALID) {
        super(parent, location)
    }
    
    /// Parse the given input into a FieldDeclaration node. If the input cannot be parsed
    /// into a FieldDeclaration node, null is returned.
    /// 
    /// #exampleInputs:
    ///     Example inputs:
    ///      * 
    ///      * 
    ///      * 
    /// 
    /// input:    The input string to parse into a FieldDeclaration instance.
    /// parent:   The parent of the FieldDeclaration node.
    /// location: The location of the input text in the source file.
    /// require:  Whether or not to throw a syntax error if the input could not
    ///           be parsed.
    /// return:   The parsed FieldDeclaration node, if successful; null otherwise.
    public static parse(String input, Node parent = null, Location location = Location.INVALID, Bool require = true) -> FieldDeclaration {
        String initializationValue = null
        String accessorValue = null
        
        var twoWay = false
        
        var accessorIndex = findAccessorIndex(input)
        
        if (accessorIndex > 0) {
            accessorValue = input.substring(accessorIndex + 2).trim()
            
            if (input[accessorIndex - 1] == '<') {
                accessorIndex--
                
                twoWay = true
            }
            
            input = input.substring(end: accessorIndex).trim()
        } else {
            var initializationValueIndex = input.indexOf('=')
            
            if (initializationValueIndex > 0) {
                initializationValue = input.substring(initializationValueIndex + 1).trim()
                input = input.substring(end: initializationValueIndex).trim()
            }
        }
        
        var declaration = InstanceDeclaration.parse(input, parent, location, require)
        
        if (declaration != null) {
            var node = new FieldDeclaration(parent, location)
            
            if (accessorValue != null) {
                node.accessorValue = PlaceholderValue.parse(accessorValue, node, location)
                node.isTwoWayAccessor = twoWay
            } else if (initializationValue != null) {
                node.initializationValue = PlaceholderValue.parse(initializationValue, node, location)
            }
            
            return (FieldDeclaration)declaration.cloneTo(node)
        }
        
        return null
    }
    
    static findAccessorIndex(String input, Int defaultReturnValue = -1) -> Int {
        var index = input.nextWordIndex("=>")
        
        if (index < 0) {
            return defaultReturnValue
        } else {
            return index
        }
    }
    
    parseAccessorValue() =>
        accessorValue.class.isOfType(PlaceholderValue) ?
            (accessorValue = Value.parse((PlaceholderValue)accessorValue)) != null :
            true
    
    parseInitializationValue() =>
        initializationValue.class.isOfType(PlaceholderValue) ?
            (initializationValue = Value.parse((PlaceholderValue)initializationValue)) != null :
            true
    
    public parsePlaceholders() =>
        parseAccessorValue() &
        parseInitializationValue()
    
    public writeInitializationValue() => initializationValue != null ? " = #initializationValue.toNova()" : ""
    public writeAccessorValue() =>
        accessorValue != null ?
            (isTwoWayAccessor ? " <=> " : " => ") + accessorValue.toNova() :
            ""
    
    public toNova() => super.toNova() + writeInitializationValue() + writeAccessorValue()
}