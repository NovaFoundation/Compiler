package "compiler/tree/nodes/variables"

import "compiler/util/Location"
import "compiler/tree/nodes/Node"
import "compiler/tree/nodes/Type"
import "compiler/tree/nodes/Identifier"
import "compiler/tree/nodes/Accessible"
import "compiler/tree/nodes/Value"
import "compiler/tree/nodes/operations/Assignable"

class Variable extends Identifier implements Accessible, Assignable {
    public Bool safeNavigation <=> safeNavigation
    public Accessible accessedNode <=> accessedNode
    
    public VariableDeclaration declaration
    
    public Type type => declaration.type
    
    /// Create a new Variable node.
    /// 
    /// parent:   The parent of the Variable node.
    /// location: The location of the input text in the source file.
    public construct(Node parent = null, Location location = Location.INVALID, VariableDeclaration declaration = null) {
        super(parent, location)
        
        this.declaration = declaration
    }
    
    [Override]
    public onAssigned(Value value) {
        ((Assignable)declaration).onAssigned(value)
    }
    
    public cloneTo(Variable other) => other {
        super.cloneTo(other)
        Accessible.super.cloneTo(other)
        
        other.declaration = declaration
    }
    
    public toNova() => declaration.name + writeArrayAccess() + writeAccessedNodes()
}