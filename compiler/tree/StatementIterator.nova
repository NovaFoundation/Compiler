package "compiler/tree"

import "nova/datastruct/list/NoSuchElementException"
import "nova/datastruct/list/Iterator"
import "nova/datastruct/list/Stack"
import "nova/regex/Pattern"

import "compiler/util/CompilerStringFunctions"
import "compiler/tree/nodes/Node"

class StatementIterator implements Iterator<String> {
    static Char[] NON_STATEMENT_CHARS = [ ' ', '\n', '\t', '\r', '}' ]
    
    visible String source
    
    visible Bool beginsScope, endsScope
    
    visible Int scopesEnded
    visible Int position
    
    Stack<Node> parents
    
    visible Bool hasNext {
        get {
            if (source.count > position) {
                for (i in position..source.count) {
                    if (!NON_STATEMENT_CHARS.contains(source[i])) {
                        return true
                    }
                }
            }
            
            return false
        }
        
        no set
    }
    
    visible String next {
        get {
            if (hasNext) {
                var prev = position
                
                position = source.nextNonWhitespaceIndex(position)
                position = source.calculateStatementEnd(position)
                position = position < 0 ? source.count : position
                
                var end = position
                
                updateScopeProperties()
                
                return source.substring(prev, end).trim()
            }
            
            throw new NoSuchElementException()

            return null
        }
        
        no set
    }
    
    updateScopeProperties() {
        var nextIndex  = source.nextNonWhitespaceIndex(position)
        var nextChar   = source[nextIndex]
        var afterIndex = source.nextNonWhitespaceIndex(nextIndex + 1)
        var charAfter  = source[afterIndex]
        
        beginsScope = position < source.count && nextChar == '{'
        endsScope   = position < source.count && nextChar == '}'
        
        scopesEnded = 0
        
        if (!endsScope && charAfter == '}') {
            endsScope = true
            position = afterIndex
        }
        
        if (endsScope) {
            var current = source.nextNonWhitespaceIndex(position)
            
            while (current > 0 && source[current] == '}') {
                scopesEnded++
                
                current = source.nextNonWhitespaceIndex(current + 1)
            }
            
            position = current < 0 ? source.count : current
        } else {
            position++
        }
    }
    
    public construct(String source, Stack<Node> parents) {
        this.source = source.trim()
        this.parents = parents
        
        reset()
    }
    
    public reset() => this { position = 0 }
}