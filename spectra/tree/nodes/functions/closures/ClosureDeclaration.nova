package "spectra/tree/nodes/functions/closures"

import "spectra/tree/nodes/Node"
import "spectra/tree/nodes/Type"
import "spectra/tree/nodes/functions/Parameter"
import "spectra/tree/nodes/functions/ParameterList"
import "spectra/tree/nodes/functions/CallableFunction"
import "spectra/tree/nodes/generics/GenericParameter"
import "spectra/util/Location"

import "spectra/util/CompilerStringFunctions"

class ClosureDeclaration extends Parameter implements CallableFunction {
    /// Create a new ClosureDeclaration node.
    /// 
    /// #exampleInputs
    /// 
    /// parent:   The parent of the ClosureDeclaration node.
    /// location: The location of the input text in the source file.
    public construct(Node parent = null, Location location = Location.INVALID) {
        super(parent, location)
        
        parameterList = new ParameterList(this)
    }
    
    /// Parse the given input into a ClosureDeclaration node. If the input cannot be parsed
    /// into a ClosureDeclaration node, null is returned.
    /// 
    /// #exampleInputs:
    ///     Example inputs:
    ///      * 
    ///      * 
    ///      * 
    /// 
    /// input:    The input string to parse into a ClosureDeclaration instance.
    /// parent:   The parent of the ClosureDeclaration node.
    /// location: The location of the input text in the source file.
    /// require:  Whether or not to throw a syntax error if the input could not
    ///           be parsed.
    /// return:   The parsed ClosureDeclaration node, if successful; null otherwise.
    public static parse(String input, Node parent = null, Location location = Location.INVALID, Bool require = true) -> ClosureDeclaration {
        var parenIndex = input.indexOf('(')
        
        if (parenIndex > 0) {
            var endIndex = input.findEndingMatch(parenIndex, '(', ')')
            
            if (endIndex > parenIndex) {
                var node = new ClosureDeclaration(parent, location)
                var name = input.substring(end: parenIndex).trim()
                
                if (node.parseName(name)) {
                    var type = findType(input, endIndex)
                    
                    if (node.parseType(type) && node.parseParameters(input.substring(parenIndex + 1, endIndex).trim(), true)) {
                        node.name = name
                        
                        return node
                    }
                }
            }
        }
        
        return null
    }
    
    public parseType(String type) => !type || super.parseType(type)
    
    static findType(String input, Int endIndex) -> String {
        var typeIndex = input.indexOf("->", start: endIndex + 1)
        
        if (typeIndex > endIndex) {
            return input.substring(typeIndex + 2).trim()
        }
        
        return null
    }
    
    /// @CallableFunction.parseParameter override that allows nameless parameters. 
    public parseParameter(String parameter, Bool require = true) =>
        ClosureParameter.parse(parameter, parameterList, require: require)
    
    public writeType() =>
        type ?
            "-> #type.toNova()" :
            ""
    
    public writeNova() => ("#{name}#parameterList.toNova() " + writeType()).trim()
}