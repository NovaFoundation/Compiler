package "spectra/tree/nodes/functions"

import "spectra/tree/nodes/Node"
import "spectra/tree/nodes/Type"
import "spectra/tree/nodes/Scope"
import "spectra/tree/nodes/Return"
import "spectra/tree/nodes/Value"
import "spectra/tree/nodes/PlaceholderValue"
import "spectra/util/Location"
import "spectra/SyntaxMessage"

import "spectra/util/CompilerStringFunctions"

class BodyFunction extends FunctionDeclaration {
    public Scope scope <=> scope
    
    visible Node inlineBody
    
    visible Bool implicitType = false
    
    /// Create a new BodyFunction node.
    /// 
    /// #exampleInputs
    /// 
    /// parent:   The parent of the BodyFunction node.
    /// location: The location of the input text in the source file.
    public construct(Node parent = null, Location location = Location.INVALID) {
        super(parent, location)
        
        scope = new Scope(this, location)
    }
    
    public addChild(Node child) {
        scope.children.add(child)
    }
    
    public parseStatement(String input, Node parent = null, Location location = Location.INVALID, Bool require = true) =>
        scope.parseStatement(input, this, location, require)
    
    /// Parse the given input into a BodyFunction node. If the input cannot be parsed
    /// into a BodyFunction node, null is returned.
    /// 
    /// #exampleInputs:
    ///     Example inputs:
    ///      * 
    ///      * 
    ///      * 
    /// 
    /// input:    The input string to parse into a BodyFunction instance.
    /// parent:   The parent of the BodyFunction node.
    /// location: The location of the input text in the source file.
    /// require:  Whether or not to throw a syntax error if the input could not
    ///           be parsed.
    /// return:   The parsed BodyFunction node, if successful; null otherwise.
    public static parse(String input, Node parent = null, Location location = Location.INVALID, Bool require = true) -> BodyFunction {
        String inlineBody = null
        var inlineBodyIndex = findInlineBodyIndex(input)
        
        if (inlineBodyIndex > 0) {
            inlineBody = input.substring(inlineBodyIndex + 2).trim()
            input = input.substring(end: inlineBodyIndex).trim()
        }
        
        var function = FunctionDeclaration.parse(input, parent, location, require)
        
        if (function != null) {
            var node = new BodyFunction(parent, location)
            
            if (inlineBody != null) {
                node.inlineBody = PlaceholderValue.parse(inlineBody, node, location, require)
            }
            
            return (BodyFunction)function.cloneTo(node)
        }
        
        if (require) {
            SyntaxMessage.error("Invalid function declaration '#input'", parent)
        }
        
        return null
    }
    
    static findInlineBodyIndex(String input, Int defaultReturnValue = -1) -> Int {
        var start = input.indexOf('(')
        
        if (start < 0) {
            return defaultReturnValue
        }
        
        var end = input.findEndingMatch(start, '(', ')')
        
        if (end < 0) {
            return defaultReturnValue
        }
        
        end = input.nextWordIndex("=>", end + 1)
        
        if (end < 0) {
            return defaultReturnValue
        } else {
            return end
        }
    }
    
    parseInlineBody() => true {
        if (inlineBody.class.isOfType(PlaceholderValue)) {
            inlineBody = scope.parseStatement(((PlaceholderValue)inlineBody).value, inlineBody.parent)
            
            if (inlineBody.class.isOfType(Value)) {
                var type = ((Value)inlineBody).type
                
                if (this.type == null) {
                    this.type = type
                    
                    implicitType = true
                } /*else if (incompatibletype) {
                    
                }*/
            }
            
            return inlineBody != null
        }
    }
    
    public parsePlaceholders() =>
        parseInlineBody() &
        scope.parsePlaceholders() &
        super.parsePlaceholders()
    
    public writeReturnType() =>
        implicitType ?
            "" :
            super.writeReturnType()
    
    writeInlineBody() =>
        "=> " + inlineBody.toNova() +
            (scope.children.count > 0 ?
                " " + scope.toNova() :
                "")
    
    public writeBody() =>
        inlineBody != null ?
            writeInlineBody() :
            scope.toNova()
    
    public writeNova() =>
        writeHeader() + " " + writeBody()
    
    public cloneTo(BodyFunction other) => other {
        super.cloneTo(other)
        
        other.scope = scope
        other.inlineBody = inlineBody
        other.implicitType = implicitType
    }
}