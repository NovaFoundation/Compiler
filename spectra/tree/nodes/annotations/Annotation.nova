package "spectra/tree/nodes/annotations"

import "spectra/tree/nodes/Node"
import "spectra/util/Location"

import "spectra/util/CompilerStringFunctions"

/// @Node extension that represents
///
/// author: Braden Steffaniak
class Annotation extends Node {
    visible String label =>
        this.class.name.endsWith("Annotation") ?
            this.class.name.trimEnd("Annotation".count) :
            null
    
    visible Bool isModifier => this.class.isOfType(Modifier) && ((Modifier)this).alias
    
    /// Create a new Annotation node.
    /// 
    /// #exampleInputs
    /// 
    /// parent:   The parent of the Annotation node.
    /// location: The location of the input text in the source file.
    public construct(Node parent = null, Location location = Location.INVALID) {
        super(parent, location)
    }
    
    /// Parse the given input into a Annotation node. If the input cannot be parsed
    /// into a Annotation node, null is returned.
    /// 
    /// #exampleInputs:
    ///     Example inputs:
    ///     * [Native]
    ///     * [Override]
    ///     * [KeepWhitespace baseline: "indent"]
    ///     * [Target not js]
    /// 
    /// input:    The input string to parse into a Annotation instance.
    /// parent:   The parent of the Annotation node.
    /// location: The location of the input text in the source file.
    /// require:  Whether or not to throw a syntax error if the input could not
    ///           be parsed.
    /// return:   The parsed Annotation node, if successful; null otherwise.
    public static parse(String input, Node parent = null, Location location = Location.INVALID, Bool require = true) -> Annotation => null {
        if (input.startsWith('[')) {
            var end = input.findEndingMatch(0, '[', ']')
            
            if (end > 1) {
                var node = null
                var contents = input.substring(1, end).trim()
                
                var bounds = contents.nextWordBounds()
                var type = contents.substring(bounds)
                var parameters = contents.substring(bounds.end).trim()
                
                until (node) {
                    node = OverrideAnnotation.parse(type, parameters, parent, location, require)
                    node = NativeAnnotation.parse(type, parameters, parent, location, require)
                    node = AbstractAnnotation.parse(type, parameters, parent, location, require)
                    node = FinalAnnotation.parse(type, parameters, parent, location, require)
                    node = PublicAnnotation.parse(type, parameters, parent, location, require)
                    node = VisibleAnnotation.parse(type, parameters, parent, location, require)
                    node = PrivateAnnotation.parse(type, parameters, parent, location, require)
                    node = StaticAnnotation.parse(type, parameters, parent, location, require)
                    node = ImmutableAnnotation.parse(type, parameters, parent, location, require)
                    node = PureAnnotation.parse(type, parameters, parent, location, require)
                    node = ImpureAnnotation.parse(type, parameters, parent, location, require)
                    node = VarAnnotation.parse(type, parameters, parent, location, require)
                    node = TargetAnnotation.parse(type, parameters, parent, location, require)
                    node = AutoFinalAnnotation.parse(type, parameters, parent, location, require)
                    node = AutoPureAnnotation.parse(type, parameters, parent, location, require)
                }
                
                return node
            }
        }
    }
    
    public static getRemainingStatement(String input) => input.substring(input.findEndingMatch(0, '[', ']') + 1).trim()
    
    public onApplied(Node appliedTo, Bool throwError = true) => true
    
    public writeNova() => "[#{label}]"
}